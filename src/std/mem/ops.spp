use std::copy::Copy
use std::mem::memory::Memory
use std::mem::ptr::Ptr
use std::number::USize
use std::number::S32
use std::option::Opt
use std::option::None
use std::option::Some
use std::single::Single
use std::void::Void


@public
@inline
@compiler_builtin
fun size_of[T]() -> USize {
    # Returns the size of a type at compile time.
}


@public
@inline
@compiler_builtin
fun size_of_value[T](value: T) -> USize {
    # Handles heap size checking for types that are not known at compile time.
}


@public
@inline
@compiler_builtin
fun mem_cmp[T](a: &T, b: &T, n: USize) -> S32 {
    # Compares two memory regions byte by byte.
}


@public
fun malloc[T](n: USize) -> Opt[Memory[T]] {
    # Determine the size and get an allocated pointer.
    let cap = n * size_of[T]()
    let raw = libc::malloc[T](cap)
    ret case raw.to_addr() == 0_uz { None() } else { Some(val=Memory(ptr=Single(raw), cap)) }
}


@public
fun calloc[T](n: USize) -> Opt[Memory[T]] {
    # Determine the size and get an allocated pointer.
    let cap = n * size_of[T]()
    let raw = libc::calloc[T](n, size_of[T]())
    ret case raw.to_addr() == 0_uz { None() } else { Some(val=Memory(ptr=Single(raw), cap)) }
}


@public
fun realloc[T](mut mem: &mut Memory[T], n: USize) -> Opt[Void] {
    # Determine the size and get a reallocated pointer.
    let cap = n * size_of[T]()
    libc::realloc(&mut mem.ptr.raw, cap)

    # If the new pointer is null, return None (failed reallocation).
    case mem.ptr.raw.to_addr() == 0_uz { ret None() }

    # Otherwise, update the length.
    mem.cap = cap
    ret Some[Void]()
}


@public
fun free[T](mem: Memory[T]) -> Void {
    # Free the memory pointed to by the pointer.
    libc::free(mem.ptr.raw)
}
