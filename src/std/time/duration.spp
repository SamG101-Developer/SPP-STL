use std::number::S64
use std::ops::add::Add
use std::ops::sub::Sub
use std::ops::mul::Mul
use std::ops::div::Div


@public
cls Duration {
    seconds: S64
    nanoseconds: S64
}


sup Duration {
    @public
    fun from_seconds(seconds: S64) -> Duration {
        ret Duration(seconds=seconds, nanoseconds=0_s64)
    }

    @public
    fun from_milliseconds(milliseconds: S64) -> Duration {
        let secs = milliseconds / 1000_s64
        let nanos = (milliseconds % 1000_s64) * 1000000_s64
        ret Duration(seconds=secs, nanoseconds=nanos)
    }

    @public
    fun from_microseconds(microseconds: S64) -> Duration {
        let secs = microseconds / 1000000_s64
        let nanos = (microseconds % 1000000_s64) * 1000_s64
        ret Duration(seconds=secs, nanoseconds=nanos)
    }

    @public
    fun from_nanoseconds(nanoseconds: S64) -> Duration {
        let secs = nanoseconds / 1000000000_s64
        let nanos = nanoseconds % 1000000000_s64
        ret Duration(seconds=secs, nanoseconds=nanos)
    }

    @public
    fun total_seconds(&self) -> S64 {
        ret self.seconds
    }

    @public
    fun total_milliseconds(&self) -> S64 {
        ret self.seconds * 1000_s64 + (self.nanoseconds / 1000000_s64)
    }

    @public
    fun total_microseconds(&self) -> S64 {
        ret self.seconds * 1000000_s64 + (self.nanoseconds / 1000_s64)
    }

    @public
    fun total_nanoseconds(&self) -> S64 {
        ret self.seconds * 1000000000_s64 + self.nanoseconds
    }
}


sup Duration ext Add[Rhs=Self, Ret=Self] {
    fun add(self, that: Self) -> Self {
        let total_nanos = self.total_nanoseconds() + that.total_nanoseconds()
        ret Duration::from_nanoseconds(total_nanos)
    }
}


sup Duration ext Sub[Rhs=Self, Ret=Self] {
    fun sub(self, that: Self) -> Self {
        let total_nanos = self.total_nanoseconds() - that.total_nanoseconds()
        ret Duration::from_nanoseconds(total_nanos)
    }
}


sup Duration ext Mul[Rhs=S64, Ret=Self] {
    fun mul(self, scalar: S64) -> Self {
        let total_nanos = self.total_nanoseconds() * scalar
        ret Duration::from_nanoseconds(total_nanos)
    }
}


sup Duration ext Div[Rhs=S64, Ret=Self] {
    fun div(self, scalar: S64) -> Self {
        let total_nanos = self.total_nanoseconds() / scalar
        ret Duration::from_nanoseconds(total_nanos)
    }
}
